*&---------------------------------------------------------------------*
*& Report ztest_oo_alv_main
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT ztest_oo_alv_main.

class lcl_report DEFINITION.

    PUBLIC SECTION.

        TYPES:BEGIN OF ty_vbak,
              vbeln TYPE vbak-vbeln,
              erdat type erdat,
              auart type auart,
              kunnr type kunnr,
              END OF TY_VBAK.

       data: t_vbak type STANDARD TABLE OF ty_vbak.

       data: o_alv type REF TO cl_salv_table.

       METHODS:
        get_data,
        generate_output.

    PRIVATE SECTION.
        METHODS:
            set_pf_status changing co_alv TYPE REF TO cl_salv_table,
            set_layout changing co_alv type REF TO cl_salv_table,
            set_top_of_page changing co_alv TYPE REF TO cl_salv_table,
            set_end_of_page changing co_alv TYPE REF TO cl_salv_table.


ENDCLASS.

START-OF-SELECTION.
    data: lo_report type ref to lcl_report.

    create OBJECT lo_report.

    lo_report->get_data( ).
    lo_report->generate_output( ).

CLASS lcl_report implementation.

    METHOD get_data.

        select vbeln erdat auart kunnr into table t_vbak FROM vbak UP TO 20 rows.

    ENDMETHOD.

    METHOD generate_output.

        data: lx_msg type REF TO cx_salv_msg.
        try.
            cl_salv_table=>factory(
                IMPORTING
                    r_salv_table = o_alv
                CHANGING
                    t_table = t_vbak
            ).
            CATCH cx_salv_msg into lx_msg.
        ENDTRY.

        call METHOD set_pf_status CHANGING co_alv = o_alv.

        call METHOD set_layout CHANGING co_alv = o_alv.

        call METHOD me->set_top_of_page CHANGING co_alv = o_alv.

        call METHOD me->set_end_of_page CHANGING co_alv = o_alv.

        o_alv->display( ).

    ENDMETHOD.

    METHOD set_pf_status.

        data: lo_functions TYPE REF TO cl_salv_functions_list.

        lo_functions = co_alv->get_functions( ).
        lo_functions->set_default( abap_true ).

        co_alv->set_screen_status(
        pfstatus = 'SALV_STANDARD'
        report = 'SALV_DEMO_TABLE_SELECTIONS'
        set_functions = co_alv->c_functions_all
        ).
    ENDMETHOD.

    METHOD set_layout.
        data: lo_layout TYPE REF TO cl_salv_layout,
              lf_variant type slis_vari,
              ls_key type salv_s_layout_key.

        lo_layout = co_alv->get_layout( ).

        ls_key-report = sy-repid.
        lo_layout->set_key( ls_key ).
        lo_layout->set_save_restriction( if_salv_c_layout=>restrict_none ).

        lf_variant = 'DEFAULT'.
        lo_layout->set_initial_layout( lf_variant ).
    ENDMETHOD.

    METHOD set_top_of_page.

        data: lo_header TYPE REF TO cl_salv_form_layout_grid,
              lo_h_label TYPE REF TO cl_salv_form_label,
              lo_h_flow TYPE REF TO cl_salv_form_layout_flow.

        CREATE OBJECT lo_header.

        lo_h_label = lo_header->create_label( row = 1 column = 1 ).
        lo_h_label->set_text( 'Header in Bold' ).

        lo_h_flow = lo_header->create_flow( row = 2 column = 1 ).
        lo_h_flow->create_text( text = 'This is text of flow' ).

        lo_h_flow = lo_header->create_flow( row = 3 column = 1 ).
        lo_h_flow->create_text( text = 'Number of Records in the output' ).

        lo_h_flow = lo_header->create_flow( row = 3 column = 2 ).
        lo_h_flow->create_text( text = 20 ).

        co_alv->set_top_of_list( lo_header ).
        co_alv->set_top_of_list_print( lo_header ).
    ENDMETHOD.

    METHOD set_end_of_page.

        data: lo_footer type REF TO cl_salv_form_layout_grid,
              lo_f_label TYPE REF TO cl_salv_form_label,
              lo_f_flow TYPE REF TO cl_salv_form_layout_flow.

        create OBJECT lo_footer.

        lo_f_label = lo_footer->create_label( row = 1 column = 1 ).
        lo_f_label->set_text( 'Footer .. here it goes' ).

        lo_f_flow = lo_footer->create_flow( row = 2 column = 1 ).
        lo_f_flow->create_text( text = 'This is text of flow in footer' ).

        lo_f_flow = lo_footer->create_flow( row = 3 column = 1 ).
        lo_f_flow->create_text( text = 'Footer number' ).

        lo_f_flow = lo_footer->create_flow( row = 3 column = 2 ).
        lo_f_flow->create_text( text = 1 ).

        co_alv->set_end_of_list( lo_footer ).
        co_alv->set_end_of_list_print( lo_footer ).
    ENDMETHOD.
ENDCLASS.